cmake_minimum_required(VERSION 3.12)
project(tests)
file(GLOB TESTS "*.cpp")
foreach (test ${TESTS})
    get_filename_component(test_src ${test} NAME)
    get_filename_component(test_nwe ${test} NAME_WE)
    include(source_lists/${test_nwe}.txt)
    add_executable(dmrg-${test_nwe} ${test_src} ${test_inc})
    set_target_properties(dmrg-${test_nwe} PROPERTIES LINK_WHAT_YOU_USE TRUE)
    target_include_directories(dmrg-${test_nwe} PRIVATE ${CMAKE_SOURCE_DIR}/source)
    target_compile_definitions(dmrg-${test_nwe} PRIVATE TEST_MATRIX_DIR="${PROJECT_SOURCE_DIR}/testmatrices")
    add_test(NAME dmrg-${test_nwe} WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR} COMMAND dmrg-${test_nwe})
    if (MSVC)
        set_target_properties(dmrg-${test_nwe} PROPERTIES LINK_FLAGS "/ignore:4099")
    endif ()
    if (DMRG_ENABLE_COVERAGE)
        target_compile_options(dmrg-${test_nwe} PRIVATE --coverage)
        target_link_options(dmrg-${test_nwe} PRIVATE --coverage)
    endif ()
    list(APPEND test_tgt_list dmrg-${test_nwe})
    target_link_libraries(dmrg-${test_nwe} PRIVATE dmrg-deps dmrg-flags)
endforeach ()

# Speed up compilation with precompiled headers
if(DMRG_ENABLE_PCH)
    if(COMMAND target_precompile_headers)
        foreach (tgt ${test_tgt_list})
            if(NOT TARGET ${first_tgt})
                target_precompile_headers(${tgt} PRIVATE <string> <vector> <array> <list> <set> <complex> <map> <optional> <memory>)
                set(first_tgt ${tgt})
            else()
                target_precompile_headers(${tgt} REUSE_FROM ${first_tgt})
            endif()
        endforeach()
    endif()
endif()

# Speed up compilation with ccache
if(DMRG_ENABLE_CCACHE)
    find_program(CCACHE_PROGRAM ccache)
    if(CCACHE_PROGRAM)
        foreach (tgt ${test_tgt_list})
            set_target_properties(${tgt} PROPERTIES CXX_COMPILER_LAUNCHER ${CCACHE_PROGRAM})
        endforeach()
        message(STATUS "Tests using ccache ${CCACHE_PROGRAM}")
        if(DMRG_ENABLE_PCH AND COMMAND target_precompile_headers)
            message(STATUS "Detected ccache + pch: Remember to set --> sloppiness = include_file_mtime,pch_defines,time_macros <-- in your ccache.conf")
        endif()
    else()
        message(STATUS "ccache program could not be found")
    endif()
endif()

#
#if (DMRG_PRINT_INFO)
#    include(${CMAKE_SOURCE_DIR}/cmake-modules/PrintTargetInfo.cmake)
#    include(${CMAKE_SOURCE_DIR}/cmake-modules/getExpandedTarget.cmake)
#    foreach(tgt ${test_tgt_list})
#        expand_target_one_level(${tgt} tgt_exp)
#        list(APPEND TEST_TARGETS_EXPANDED ${tgt_exp})
#    endforeach()
#    list(REMOVE_DUPLICATES TEST_TARGETS_EXPANDED)
#    message(STATUS "| TEST TARGET SUMMARY")
#    message(STATUS "|--------------------")
#    print_compiler_info("| ")
#    foreach(tgt ${TEST_TARGETS_EXPANDED})
#        print_target_info(${tgt} "| ")
#    endforeach()
#endif()
#
## Print summary of CMake configuration
#if (DMRG_PRINT_INFO)
#    include(${CMAKE_SOURCE_DIR}/cmake-modules/PrintTargetInfo.cmake)
#    include(${CMAKE_SOURCE_DIR}/cmake-modules/getExpandedTarget.cmake)
#    message(STATUS "| TEST TARGET SUMMARY")
#    message(STATUS "|--------------------")
#    print_compiler_info("| ")
#    expand_target_all_targets(all-tests TEST_TARGET_EXPANDED)
#    foreach(tgt ${TEST_TARGET_EXPANDED})
#        print_target_info(${tgt} "| ")
#    endforeach()
#endif()
