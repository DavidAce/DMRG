function(find_quadmath)
    find_library(QUADMATH_LIBRARY NAMES quadmath)
    find_path(QUADMATH_INCLUDE_DIR NAMES quadmath.h PATH_SUFFIXES include)
    if(NOT QUADMATH_LIBRARY OR NOT QUADMATH_INCLUDE_DIR)
        include(CMakePushCheckState)
        include(CheckCXXSourceCompiles)
        include(CheckTypeSize)
        cmake_push_check_state(RESET)
        set(CMAKE_EXTRA_INCLUDE_FILES "quadmath.h")
        set(CMAKE_REQUIRED_LIBRARIES "quadmath")
        set(CMAKE_REQUIRED_QUIET TRUE)
        check_type_size(__float128 SIZEOF__float128 BUILTIN_TYPES_ONLY LANGUAGE CXX)
        check_cxx_source_compiles("
            #include <quadmath.h>
            int main(void){
                __float128 foo = ::sqrtq(123.456);
            }"
            QUADMATH_LINK_ONLY
        )
        cmake_pop_check_state()
        if (QUADMATH_LINK_ONLY)
            set(QUADMATH_INCLUDE_DIR "unused" CACHE PATH "" FORCE)
            set(QUADMATH_LIBRARY "quadmath" CACHE FILEPATH "" FORCE)
        elseif(CMAKE_CXX_COMPILER_ID MATCHES "Clang")
            if(NOT quadmath_FIND_QUIETLY)
                message(STATUS "Failed to compile a simple quadmath program: Note that Clang does not support quadmath.")
            endif()
        else()
            if(NOT quadmath_FIND_QUIETLY)
                message(STATUS "Failed to compile a simple quadmath program.")
            endif()
        endif()
    endif()
endfunction()

find_quadmath()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(quadmath DEFAULT_MSG QUADMATH_LIBRARY QUADMATH_INCLUDE_DIR HAVE_SIZEOF__float128)

if(quadmath_FOUND)
    if(NOT TARGET quadmath::quadmath)
        if(QUADMATH_LINK_ONLY)
#            message(STATUS "Defining INTERFACE target quadmath::quadmath")
            add_library(quadmath::quadmath INTERFACE IMPORTED)
            target_link_libraries(quadmath::quadmath INTERFACE ${QUADMATH_LIBRARY})
        else()
#            message(STATUS "Defining UNKNOWN target quadmath::quadmath")
            add_library(quadmath::quadmath UNKNOWN IMPORTED)
            set_target_properties(quadmath::quadmath PROPERTIES IMPORTED_LOCATION "${QUADMATH_LIBRARY}")
            if(QUADMATH_INCLUDE_DIR AND NOT QUADMATH_INCLUDE_DIR MATCHES "unused")
                target_include_directories(quadmath::quadmath SYSTEM INTERFACE ${QUADMATH_INCLUDE_DIR} )
            endif()
        endif()
        message(DEBUG "Defined target quadmath::quadmath for library: ${QUADMATH_LIBRARY}")
    endif()
endif()