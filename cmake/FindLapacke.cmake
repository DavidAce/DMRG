
function(find_Lapacke)
    include(cmake/CheckCompile.cmake)
    set(lapacke_tgt_candidates mkl::mkl OpenBLAS::OpenBLAS LAPACK::LAPACK)
    foreach(tgt ${lapacke_tgt_candidates})
        if(TARGET ${tgt})
            message(DEBUG "Looking for Lapacke in ${tgt}")
            if(EXISTS ${PROJECT_SOURCE_DIR}/cmake/compile/Lapacke.cpp)
                check_compile(Lapacke ${tgt} ${PROJECT_SOURCE_DIR}/cmake/compile/Lapacke.cpp)
                if(check_compile_Lapacke)
                    add_library(lapacke::lapacke INTERFACE IMPORTED)
                    target_link_libraries(lapacke::lapacke INTERFACE ${tgt})
                    message(DEBUG "Looking for Lapacke in ${tgt} - found")
                    break()
                else()
                    unset(Compile_${Lapacke})
                endif()
            else()
                add_library(lapacke::lapacke INTERFACE IMPORTED)
                target_link_libraries(lapacke::lapacke INTERFACE ${tgt})
                message(DEBUG "Assuming Lapacke is in ${tgt}")
                break()
            endif()
        endif()
    endforeach()
endfunction()

find_Lapacke()
if(TARGET lapacke::lapacke)
    set(LAPACKE_TARGET lapacke::lapacke)
endif()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(Lapacke DEFAULT_MSG LAPACKE_TARGET)